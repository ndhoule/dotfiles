# -*- mode: sh -*-

#
# Executes commands at the start of an interactive session.
#
# Author:
#   Nathan Houle <nathan@nathanhoule.com>
#
# Credit to:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Zach Holman <zach@zachholman.com>
#

function {
  export DOTFILES="${HOME}/.dotfiles"
  export DEV_PATH="${HOME}/dev"
  export SRC_PATH="${DEV_PATH}/src"

  #
  # Zprezto
  #

  # Load zprezto if it's installed
  if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
    source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
  fi

  #
  # Zsh submodules
  #

  # All zsh config files (all *.zsh files contained in $DOTFILES children
  # directories). Note that this glob is limited to one directory level deep.
  local -a config_files
  config_files=(${DOTFILES}/*/*.zsh)

  # Load path files
  for file in ${(M)config_files:#*/path.zsh}; do
    source $file
  done

  # Load all files except for path, completions, and host-specific files
  for file in ${config_files:#*/(path.zsh|completion.zsh|host/*.zsh)}; do
    source $file
  done

  # Load host-specific files
  for file in ${(M)config_files:#*/host/*.zsh}; do
    if [[ "$(basename $file | cut -d '.' -f 1)" == "$(hostname | cut -d '.' -f 1)" ]]; then
      source $file
    fi
  done

  # Load completions
  for file in ${(M)config_files:#*/completion.zsh}; do
    source $file
  done
}
